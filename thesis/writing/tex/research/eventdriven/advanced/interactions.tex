\subsection{Synchronous Agent Interactions}
With the concepts introduced so far we can achieve already a lot in terms of agent interactions. Agents can react to incoming events, which are either the \texttt{Tick} event advancing simulation time by one step or a message sent by another agent, or the agent itself. This is enough to implement simple one-directional agent interactions where one agent sends a message to another agent but does not await an answer within the same \texttt{Tick}. One-directional interactions are used in the model to implement the passing on of diseases, the paying back of debt and passing on of wealth to children upon death, where in all cases the agent simply sends a message and forgets about it.

Unfortunately, this mechanism is not enough to implement the other agent interactions in the Sugarscape model, which are structurally richer as they need to be synchronous. In the use cases of mating, trading and lending, two agents need to come to an agreement over multiple interaction steps within the same \texttt{Tick} which need to be exclusive and synchronous. This means that an agent A initiates such a multi-step conversation with another agent B by sending an initial message to which agent B has to react by a reply to agent A who upon reception of the message, will pick up computation from that point and reply with a new message and so on. Both agents must not interact with other agents during this conversation to guarantee resource constraints, otherwise it would become quite difficult and cumbersome to ensure that agents don't spend more than they have when trading with multiple other agents at the same time. Also, the initiating agent A must be able to pick up processing of its \texttt{Tick} event from the point where it started the conversation with agent B because sending a message always requires handing the control back to the simulation kernel. See Figure \ref{fig:syncagentinteractions} for a visualisation of the sequence of actions.

\begin{figure}
	\centering
	\includegraphics[width=1.0\textwidth, angle=0]{./fig/eventdriven/syncagentinteractions.png}
	\caption[Sequence diagram of synchronous agent interaction in the trading use case]{Sequence diagram of synchronous agent interaction in the trading use case. Upon the handling of the \texttt{Tick} event, agent A looks for trading partners and finds agent B within its neighbourhood and sends a \texttt{TradeOffer} message. Agent B replies to this message with \texttt{TradeReply} and agent A continues with the trading algorithm by picking up where it has left the execution when sending the message to agent B. After agent A has finished the trading with agent B, it turns to agent C, where the same procedure follows.}
	\label{fig:syncagentinteractions}
\end{figure}

The way to implement this is to allow an agent to be able to change its internal event-handling state by switching into different event-handlers, after having sent an event, to be able to react to the incoming reply in a specific way by encapsulating local state for the current synchronous interaction through closures and currying. By making use of continuations, the agent can pick up the processing of the \texttt{Tick} event after the synchronous agent interaction has finished. Key to this is the function \texttt{continueWithAfter} which we already shortly introduced through \texttt{generalEventHandler} in the previous section. This function takes an \texttt{MSF} which returns an output of type \texttt{b} and an optional \texttt{MSF}. If this optional \texttt{Maybe MSF} is \texttt{Just} then the \textit{next} input is handled by this new \texttt{MSF}. In case no new \texttt{MSF} is returned (\texttt{Nothing}), the \texttt{MSF} will stay the same. This is a more specialised version of the \texttt{switch} combinator introduced in Chapter \ref{sec:back_frp} in the way that it doesn't need an additional function to produce the actual \texttt{MSF} continuation. The semantics are different though. Whereas \texttt{switch} runs the new \texttt{MSF} immediately, \texttt{continueWithAfter} only applies the new \texttt{MSF} in the \textit{next} step. The implementation of the function is as follows:

\begin{HaskellCode}
continueWithAfter :: Monad m => MSF m a (b, Maybe (MSF m a b)) -> MSF m a b
continueWithAfter msf = MSF (\a -> do
  ((b, msfCont), msf') <- unMSF msf a
  let msfNext = fromMaybe (continueWithAfter msf') msfCont
  return (b, msfNext))
\end{HaskellCode}

Finally, we can discuss the \texttt{Tick} handling function. It returns a value of type \texttt{Maybe (EventHandler g)} which if \texttt{Just} will result in a change of the top-level event handler through \texttt{continueWithAfter} as shown in \texttt{generalEventHandler} above. Note the use of continuations in the case of \texttt{agentMating, agentTrade} and \texttt{agentLoan}. All these functions return a \texttt{Maybe (EventHandler g)} because all of them can potentially result in synchronous agent interactions which require changing the top-level event handler. The function \texttt{agentDisease} is the last in the chain of agent behaviour, thus we are passing a default continuation which simply switches back into \texttt{generalEventHandler} to finish the processing of a \texttt{Tick} in an agent.

\begin{HaskellCode}
handleTick :: RandomGen g => DTime -> AgentLocalMonad g (Maybe (EventHandler g))
handleTick dt = do
  -- perform ageing of agent
  agentAgeing dt
  -- agent move, returns amount it of resources it harvested
  harvestAmount <- agentMove
  -- metabolise resources, depending on agents metabolism rate
  -- returns amount metabolised
  metabAmount <- agentMetabolism
  -- polute environment given harvest and metabolism amount
  agentPolute harvestAmount metabAmount
  -- check if agent has starved to death or died of age
  ifThenElseM
    (starvedToDeath `orM` dieOfAge)
    (do
      -- died of age or starved to deat: remove from simulation
      agentDies agentMsf
      return Nothing) 
    -- still alive, perform the remaining steps of the behaviour
    -- pass agentContAfterMating as continuation to pick up after mating
    -- synchronous conversations have finished
    (agentMating agentMsf agentContAfterMating)

-- after mating continue with cultural process and trading
agentContAfterMating :: RandomGen g 
                     => AgentLocalMonad g (Maybe (EventHandler g))
agentContAfterMating = do
    agentCultureProcess
    -- pass agentContAfterTrading as continuation to pick up after trading 
    -- synchronous conversations have finished
    agentTrade agentContAfterTrading 

-- after trading continue with lending and borrowing
agentContAfterTrading :: RandomGen g 
                      => AgentLocalMonad g (Maybe (EventHandler g))
agentContAfterTrading = agentLoan agentContAfterLoan

-- after lending continue with diseases, which is the step in a Tick event
agentContAfterLoan :: RandomGen g 
                   => AgentLocalMonad g (Maybe (EventHandler g))
agentContAfterLoan = agentDisease defaultCont

-- safter diseases imply switch back into the general event handler
defaultCont :: RandomGen g => AgentLocalMonad g (Maybe (EventHandler g))
defaultCont = return (Just generalEventHandler)
\end{HaskellCode}

\subsubsection{Tagless final}
Although the indirect, continuation-based approach to synchronous agent interactions as shown before works, it is quite cumbersome, fragile and it is easy to get something wrong. What would be more desirable is to have a truly synchronous approach, where the reply to an event happens directly as a result of the \texttt{sendEventTo} function. In this case, when calling \texttt{sendEventTo}, behind the scenes the receiving agent is executed and the result is returned directly to the caller, without any indirections. With the \textit{tagless final} approach as introduced in the event-driven SIR in section \ref{sec:tagless_final_basics}, this becomes possible in an elegant and robust way. We have developed this concept for the event-driven SIR only. It should be equally applicable for the Sugarscape, but we leave that for further research. We start by extending the API by defining a \textit{new} type class \texttt{MonadAgentSync}:

\begin{HaskellCode}
class Monad m => MonadAgentSync e m | m -> e where
  sendSync :: e -> AgentId -> m (Maybe [e])
\end{HaskellCode}

The semantics behind the \texttt{sendSync} method are that it allows to send an event of type \texttt{e} to agent with the id \texttt{AgentId}. If the agent cannot be found it will return \texttt{Nothing}. Otherwise it will return \texttt{Just} the list of events the receiving agent replies to the sending agent. 

The only place which has to be changed is the susceptible agent but due to the different semantics, parts of its behaviour needs to be rewritten. The receiving agents are left unchanged because at the moment a receiver has no means to distinguish between asynchronous and synchronous events and is not forced to reply to the sender in case of a synchronous event. It would be useful to have some mechanism that in case of a synchronous event, the receiver can only reply to the sender. We leave that issue for further research.

Handling an incoming \texttt{Contact} from an \texttt{Infected} agent is no longer necessary as it will not happen, because the interactions go directly through \texttt{sendSync} and infected agents don't make contact proactively. Thus, the \texttt{MakeContact} handler has to be changed to take into account that the infection can happen directly there:

\begin{HaskellCode}
handleEvent MakeContact = do
  ais        <- getAgentIds
  ai         <- getMyId
  isInfected <- makeContact beta ai ais
  if isInfected
    -- got infected, signal event to switch
    then return (Just ())
    else do
      -- not infected, reschedule MakeContact
      scheduleMakeContact
      return Nothing
\end{HaskellCode}

The function \texttt{makeContact} recursively \texttt{makeContactWith} $\beta$ (contact rate) other agents. Whereas previously, sending a \texttt{Contact} event to itself was not a problem, this is not allowed anymore and must be prevented explicitly. The reason for that is discussed below when introducing the \texttt{sendSync} method of the pure interpreter. Sending to itself counts against the $\beta$ contacts, as it would make no difference as receiving a \texttt{Contact} from a \texttt{Susceptible} has no effect on a susceptible agent anyway. If the case arises in a model that agents need to send events to themselves, it cannot happen through mechanisms like \texttt{sendSync} but it must go through the normal scheduling of events which decouples sending from receiving.

\begin{HaskellCode}
makeContact :: (MonadAgent SIREvent m, MonadAgentSync SIREvent m)
            => Int       -- number of contacts to make
            -> AgentId   -- sender agent id
            -> [AgentId] -- all agent ids (including self)
            -> m Bool
makeContact 0 _ _ = return False
makeContact n ai ais = do
  receiver <- randomElem ais
  -- prevent sending to self
  if ai == receiver
    -- self counts against beta contacts
    then makeContact (n-1) ai ais
    else do
      -- make contact
      ret <- makeContactWith receiver
      if ret
        -- got infected, stop
        then return True
        -- not infected, continue
        else makeContact (n-1) ai ais
\end{HaskellCode}

Finally we can use \texttt{sendSync} to directly send events to a receiving agent, which replies with a list of events to the sender. We need to add the new \texttt{MonadAgentSync} type class to the overloaded function, to make the \texttt{sendSync} method available.

\begin{HaskellCode}
makeContactWith :: (MonadAgent SIREvent m, MonadAgentSync SIREvent m) 
                => AgentId -> m Bool
makeContactWith receiver = do
  ai     <- getMyId
  -- DIRECT SYNCHRONOUS AGENT INTERACTION HAPPENS HERE
  retMay <- sendSync (Contact ai Susceptible) receiver
  case retMay of 
    -- receiver not found, no infection
    Nothing -> return False
    -- receiver found, replied with es events
    (Just es) -> do
      -- check if any event in replies is from Infected
      let fromInf = any (\ (Contact _ s) -> s == Infected) es
      if not fromInf
        -- none from Infected, no infection
        then return False
        -- at least one from Infected, might become infected
        else do
          r <- randomBool inf
          if r 
            -- got infected, become infected
            then do
              scheduleRecoveryM ild
              return True
            else return False
\end{HaskellCode}

The \texttt{sendSync} method needs to be implemented in a new \textit{pure} interpreter, which follows exactly the same concept as scheduling an event so we briefly discuss it conceptually. The method looks up the receiving agent, runs it with the given event of the sender and filters the replies to the sending agent. To do that, the method needs to have all agents available to actually execute them. This is achieved by keeping the agent mappings in the \texttt{SimState}. They are managed using a \texttt{State} Monad and thus can be read and written, both of which are necessary, as after a successful run of the receiving agent, its new \texttt{MSF} needs to be put back into the agent mappings.

Now it becomes clear why an agent cannot send an event with \texttt{sendSync} to itself and why circular \texttt{sendSync} (agent A \texttt{sendSync} to agent B \texttt{sendSync} to agent C \texttt{sendSync} to agent A) are also not allowed. The new \texttt{MSF} of an agent which was just run and updated in the \texttt{SimState} will be overridden by the subsequent updates of runs which were initiated earlier. Fortunately, this can be conveniently checked within the \texttt{sendSync} method and an error or exception can be generated which is better than silently ignoring it, resulting in unexpected behaviour. Because the initiating agent's id is always known, and because it is easy to keep track of the agents ids currently engaged in a \texttt{sendSync} by storing their ids in the \texttt{SimState} it is possible to check this at run time, arriving at some kind of call stack management.

The idea of circular event sending is closely related to the concept of \textit{open recursion} from object-oriented programming, where a method of an object can invoke another method of the same object directly or indirectly and still mutate that object. With our approach this is simply not possible due to the lack of mutable data and side effects. 

Although the tagless final approach makes things easier under certain circumstances, it comes also with subtle drawbacks, thus it depends on the model semantics which approach to synchronous agent interactions should be chosen. Still we think that this approach is another demonstration of the usefulness of a tagless final approach. We have shown how to extend the existing API with new operations without breaking the existing implementation. Also we think that we only scratched the surface with this approach of direct synchronous agent interactions but we leave a more in-depth exploration of it for further research.

% direct MSF call, Problem is recursive nature. maybe try it with gintis Implementation. I just learned that what i want to achieve is actually: https://en.wikipedia.org/wiki/This_(computer_programming)#Open_recursion AND OPEN RECURSION IS PRETTY BAD