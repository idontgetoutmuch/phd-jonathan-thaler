\section{Property-Based Testing}
\label{sec:proptesting}
In property-based testing \textit{functional specifications}, also called properties, are formulated in code and tried to falsify using a property-based testing library. In general, to falsify a functional specification, the property-based testing library runs \textit{automated} test cases by \textit{automatically} generating test data. When a test case fails, the functional specification was falsified by finding a counter example. For better analysis, the library then reduces the test data to its simplest form for which the test still fails, like shrinking of a list or pruning of a tree. On the other hand, if no counter example could be found for the functional specification, it is deemed valid and the test succeeds.

Property-based testing has its origins in the QuickCheck library \cite{claessen_quickcheck_2000,claessen_testing_2002} of the pure functional programming language Haskell. QuickCheck tries to falsify the specifications by \textit{randomly} sampling the test space. This library has been successfully used for testing Haskell code in the industry for years, underlining its maturity and real world relevance in general and of property-based testing in particular \cite{hughes_quickcheck_2007}.

To give an understanding of how property-based testing works with QuickCheck, we give a practical example of how to implement a property of lists. Such a property is directly expressed as a function in Haskell, with the return type of \texttt{Bool}. This indicates whether the property holds for the given random inputs or not. In general, a QuickCheck property can take arbitrary inputs, with random data generated automatically by QuickCheck during testing. The example property we want to encode is that reversing a reversed list results again in the original list:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
-- Reversing of a reversed list is the original list
prop_reverse_reverse :: [Int] -> Bool
prop_reverse_reverse xs = reverse (reverse xs) == xs
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

Testing the property with QuickCheck is simply done using the function \\ \texttt{quickCheck}:

\begin{footnotesize}
\begin{verbatim}
> quickCheck prop_reverse_reverse
+++ OK, passed 100 tests.
\end{verbatim}
\end{footnotesize}

QuickCheck generates 100 test cases by default and requires all of them to pass. Indeed, all 100 test cases of \texttt{prop\_reverse\_reverse} pass and therefore the property as a whole passes the test. Note that we do not provide any data for the input argument \texttt{[Int]}, a list of Integers, because QuickCheck is doing this automatically for us. For the standard types of Haskell, QuickCheck provides existing data generators.

To give an example of what happens in case of failure due to a  wrong property, we look at a wrong implementation of the property, that reverse distributes over the list append operator (\texttt{++} in Haskell):

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
-- reverse is distributive over list append (++)
-- This is a wrong implementation for explanatory reasons!
-- For a correct property, swap xs and ys on the right hand side.
prop_reverse_distributive :: [Int] -> [Int] -> Bool
prop_reverse_distributive xs ys 
  = reverse (xs ++ ys) == reverse xs ++ reverse ys
  
> quickCheck prop_reverse_distributive
*** Failed! Falsifiable (after 4 tests and 5 shrinks):    
[0]
[1]
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

As expected, the property test fails because QuickCheck found a counter example to the property after 4 test cases. Also, we see that QuickCheck applied 5 shrinks to find the minimal failing counter example \texttt{xs = [0]} and \texttt{ys = [1]}. The reason for the failure is a wrong implementation of the \texttt{prop\_reverse\_distributive} property: to correct it, \texttt{xs} and \texttt{ys} need to be swapped on the right hand side of the equation. Note that when run repeatedly, QuickCheck might find the counter example earlier and might apply fewer shrinks due to a different random-number generator seed, resulting in different random data to start with.

\subsection{Generators}
QuickCheck comes with a lot of data generators for existing types like \texttt{String, Int, Double, [] (List)}, but in case one wants to randomize custom data types, one has to write custom data generators. There are two ways to do this. The first one is to fix them at compile time by writing an \texttt{Arbitrary} type class instance. A type class can be understood as an interface definition, and an instance as a concrete implementation of such an interface for a specific type. The advantage of having an \texttt{Arbitrary} instance is that the custom data type can be used as random argument to a function as in the examples above. The second way to write custom data generators is to implement a run-time generator in the \texttt{Gen} context.

Here we implement a custom data generator for both cases, using a simple color representation as example. We start with the run-time option, running in the \texttt{Gen} context:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
-- enumeration of colors
data Color = Red | Green | Blue

genColor :: Gen Color
genColor = elements [Red, Green, Blue]
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

This implementation makes use of the \texttt{elements :: [a] $\rightarrow$ Gen a} function, which picks a random element from a non-empty list with uniform probability. If a skewed distribution is needed, one can use the \texttt{frequency :: [(Int, Gen a)] $\rightarrow$ Gen a} function, where a frequency can be specified for each element. Generating on average 80\% \texttt{Red}, 15\% \texttt{Green} and 5\% \texttt{Blue} can be achieved using this function:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
genColor :: Gen Color
genColor = frequency [(80, Red), (15, Green), (5, Blue)]
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

Implementing an \texttt{Arbitrary} instance is straightforward, one only needs to implement the \texttt{arbitrary :: Gen a} method:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
instance Arbitrary Color where
  arbitrary = genColor
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

When we have a random \texttt{Double} as input to a function, but want to restrict its random range to (0,1) because it reflects a probability, we can do this easily with \texttt{newtype} and implementing an \texttt{Arbitrary} instance:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
newtype Probability = P Double

instance Arbitrary Probability where
  arbitrary = P <$> choose (0, 1)
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

\subsection{Distributions}
QuickCheck provides functions to measure the coverage of test cases. This can be done using the 
\texttt{label :: String $\rightarrow$ prop $\rightarrow$ Property} function. It takes a \texttt{String} as first argument and a testable property and constructs a \texttt{Property}. QuickCheck collects all the generated labels, counts their occurrences and reports their distribution. For example, it can be used to get an idea of the length of the random lists created in the \texttt{reverse\_reverse} property shown above:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
reverse_reverse_label :: [Int] -> Property
reverse_reverse_label xs  
  = label ("length of random-list is " ++ show (length xs)) 
          (reverse (reverse xs) == xs)
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

When running the test, we get the following output:

\begin{footnotesize}
\begin{verbatim}
+++ OK, passed 100 tests:
 5% length of random-list is 27
 5% length of random-list is 0
 4% length of random-list is 19
 ...
\end{verbatim}
\end{footnotesize}

\subsection{Coverage}
QuickCheck provides two additional functions to work with test-case distributions: \texttt{cover} and \texttt{checkCoverage}. The function \texttt{cover :: Double $\rightarrow$ Bool $\rightarrow$ String $\rightarrow$ prop $\rightarrow$ Property} allows to explicitly specify that a given percentage of successful test cases belongs to a given class. The first argument is the expected percentage, the second argument is a \texttt{Bool} indicating whether the current test case belongs to the class or not, the third argument is a label for the coverage, and the fourth argument is the property which needs to hold for the test case to succeed. 

Here we look at an example where we use \texttt{cover} to express that we expect 15\% of all test cases to have a random list with at least 50 elements:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
reverse_reverse_cover :: [Int] -> Property
reverse_reverse_cover xs  
  = cover 15 (length xs >= 50) "Length of random list at least 50"
             (reverse (reverse xs) == xs)
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

When running the twice, we get the following output:

\begin{footnotesize}
\begin{verbatim}
+++ OK, passed 100 tests (10% length of random list at least 50).
Only 10% Length of random-list at least 50, but expected 15%.
+++ OK, passed 100 tests (21% length of random list at least 50).
\end{verbatim}
\end{footnotesize}

As can be seen, QuickCheck runs the default 100 test cases and prints a warning if the expected coverage is not reached. This is a useful feature, but it is up to us to decide whether 100 test cases are suitable and whether we can really claim that the given coverage will be reached or not. To free us from making this guess, QuickCheck provides the function \texttt{checkCoverage :: prop $\rightarrow$ Property}. When \texttt{checkCoverage} is used, QuickCheck will run an increasing number of test cases until it can decide whether the percentage in \texttt{cover} was reached or cannot be reached at all. The way QuickCheck does this, is by using sequential statistical hypothesis testing \cite{wald_sequential_1992}. Thus, if QuickCheck comes to the conclusion that the given percentage can or cannot be reached, it is based on a robust statistical test giving us high confidence in the result.

When we run the example from above but now with \texttt{checkCoverage} we get the following output:

\begin{footnotesize}
\begin{verbatim}
+++ OK, passed 12800 tests 
    (15.445% length of random-list at least 50).
\end{verbatim}
\end{footnotesize}

We see that after QuickCheck ran 12,800 tests it came to the statistically robust conclusion that, indeed, at least 15\% of the test cases have a random list with at least 50 elements. 