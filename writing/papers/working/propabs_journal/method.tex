\section{Encoding Agent Specifications}
\label{sec:method}
We start by encoding the invariants of the susceptible agent directly into Haskell, implementing a function, which takes all necessary parameters and returns a \texttt{Bool} indicating whether the invariants hold or not. We are using pattern matching, therefore it reads like a formal specification due to the declarative nature of functional programming.

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
susceptibleProps :: SIREvent              -- random event sent to agent
                 -> SIRState              -- output state of the agent
                 -> [QueueItem SIREvent]  -- list of events the agent scheduled
                 -> AgentId               -- agent id of the agent
                 -> Bool
-- received Recover => stay Susceptible, no event scheduled
susceptibleProps Recover Susceptible es _ = null es
-- received Contact _ Recovered => stay Susceptible, no event scheduled
susceptibleProps (Contact _ Recovered) Susceptible es _ = null es
-- received Contact _ Susceptible => stay Susceptible, no event scheduled
susceptibleProps (Contact _ Susceptible) Susceptible es _  = null es
-- received Contact _ Infected, didn't get Infected, no event scheduled
susceptibleProps (Contact _ Infected) Susceptible es _ = null es
-- received Contact _ Infected AND got infected, check events
susceptibleProps (Contact _ Infected) Infected es ai
  = checkInfectedInvariants ai es
-- received MakeContact => stay Susceptible, check events
susceptibleProps MakeContact Susceptible es ai
  = checkMakeContactInvariants ai es cor
-- all other cases are invalid and result in a failed test case
susceptibleProps _ _ _ _ = False
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

Next, we give the implementation for the \texttt{checkInfectedInvariants} function. We omit a detailed implementation of \texttt{checkMakeContactInvariants} as it works in a similar way and its details do not add anything conceptually new. The function \texttt{checkInfectedInvariants} encodes the invariants which have to hold when the susceptible agent receives a \texttt{(Contact \_ Infected)} event from an infected agent and becomes infected.

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
checkInfectedInvariants :: AgentId               -- agent id of the agent 
                        -> [QueueItem SIREvent]  -- list of scheduled events
                        -> Bool
checkInfectedInvariants sender 
  -- expect exactly one Recovery event
  [QueueItem receiver (Event Recover) t'] 
  -- receiver is sender (self) and scheduled into the future
  = sender == receiver && t' >= t 
-- all other cases are invalid
checkInfectedInvariants _ _ = False
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

%The \texttt{checkMakeContactInvariants} is a bit more complex:
%
%%%\begin{HaskellCode}
%checkMakeContactInvariants :: AgentId              -- ^ Agent id of the agent 
%                           -> [QueueItem SIREvent] -- ^ Events the agent scheduled
%                           -> Int                  -- ^ Contact Rate
%                           -> Bool
%checkMakeContactInvariants sender es contactRate
%    -- make sure there has to be exactly one MakeContact event and
%    -- exactly contactRate Contact events
%    = invOK && hasMakeCont && numCont == contactRate
%  where
%    (invOK, hasMakeCont, numCont) 
%      = foldr checkMakeContactInvariantsAux (True, False, 0) es
%
%    checkMakeContactInvariantsAux :: QueueItem SIREvent 
%                                  -> (Bool, Bool, Int)
%                                  -> (Bool, Bool, Int)
%    checkMakeContactInvariantsAux 
%        (QueueItem (Contact sender' Susceptible) receiver t') (b, mkb, n)
%      = (b && sender == sender'   -- sender in Contact must be self
%           && receiver `elem` ais -- receiver of Contact must be in agent ids
%           && t == t', mkb, n+1)  -- Contact event is scheduled immediately
%    checkMakeContactInvariantsAux 
%        (QueueItem MakeContact receiver t') (b, mkb, n) 
%      = (b && receiver == sender  -- receiver of MakeContact is agent itself
%           && t' == t + 1         -- MakeContact scheduled 1 timeunit into future
%           &&  not mkb, True, n)  -- there can only be one MakeContact event
%    checkMakeContactInvariantsAux _ (_, _, _) 
%      = (False, False, 0)         -- other patterns are invalid
%%\end{HaskellCode}

\subsection{Writing a Property Test}
After having encoded the invariants into a function, we need to write a QuickCheck property test, which calls this function with random test data. Although QuickCheck comes with a lot of data generators for existing Haskell types, it obviously does not have generators for custom types, like the \texttt{SIRState} and \texttt{SIREvent}. We refer to section \ref{sec:proptesting}, where we explain the concept of data generators and implement generators for \texttt{Color} and \texttt{Probability}. The run-time generators for \texttt{SIRState} and \texttt{genEvent} for generating random \texttt{SIREvents} work similar to the \texttt{Color} generator and is omitted. For readers who are interested in a detailed implementation of both, we refer to the code repository \cite{thaler_repository_2019}.

All parameters to the property test are generated randomly, which expresses that the properties encoded in the previous section have to hold invariant of the model parameters. We make use of additional data generator modifiers: \texttt{Positive} ensures that a value generated is positive; \texttt{NonEmptyList} ensures that a randomly generated list is not empty. Further, we use the function \texttt{label}, as explained in section \ref{sec:proptesting}, to get an understanding of the distribution of the transitions. The case where the agents output state is \texttt{Recovered} is marked as "INVALID" as it must never occur, otherwise the test will fail, due to the invariants encoded in the previous section.

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
prop_susceptible :: Positive Int         -- beta (contact rate)
                 -> Probability          -- gamma (infectivity)
                 -> Positive Double      -- delta (illness duration)
                 -> Positive Double      -- current simulation time
                 -> NonEmptyList AgentId -- population agent ids
                 -> Gen Bool
prop_susceptible 
  (Positive beta) (P gamma) (Positive delta) (Positive t) (NonEmpty ais) = do
  -- generate random event, requires the population agent ids
  evt <- genEvent ais
  -- run susceptible random agent with given parameters (implementation omitted)
  (ai, ao, es) <- genRunSusceptibleAgent beta gamma delta t ais evt
  -- check properties
  return (label (labelTestCase ao) (susceptibleProps evt ao es ai))
  where
    labelTestCase :: SIRState -> String
    labelTestCase Infected    = "Susceptible -> Infected"
    labelTestCase Susceptible = "Susceptible"
    labelTestCase Recovered   = "INVALID"
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

We have omitted the implementation of \texttt{genRunSusceptibleAgent} as it would require the discussion of implementation details of the agent. Conceptually speaking, it executes the agent with the respective arguments with a fresh random-number generator and returns the agent id, its state and scheduled events.

Finally, we run the test using QuickCheck. Due to the large random sampling space with 5 parameters, we increase the number of test cases to 100,000.

\begin{footnotesize}
\begin{verbatim}
> quickCheckWith (stdArgs {maxSuccess=100000}) prop_susceptible
+++ OK, passed 100000 tests (6.77s):
94.522% Susceptible
 5.478% Susceptible -> Infected
\end{verbatim}
\end{footnotesize}

All 100,000 test cases pass, taking 6.7 seconds to run on our hardware. The distribution of the transitions shows that we indeed cover both cases a susceptible agent can exhibit within one event. It either stays susceptible or makes the transition to infection. The fact that there is no transition to \texttt{Recovered} shows that the implementation is correct.

Encoding of the invariants and writing property tests for the infected agent follows the same idea and is not repeated here. Next, we show how to test transition probabilities using the powerful statistical hypothesis testing feature of QuickCheck.

\subsection{Encoding Transition Probabilities}
In the specifications from the previous section there are probabilistic state transitions, for example the susceptible agent \textit{might} become infected, depending on the events it receives and the infectivity ($\gamma$) parameter. To encode these probabilistic properties we are using the function \texttt{cover} of QuickCheck. As introduced in section \ref{sec:proptesting}, this function allows us to explicitly specify that a given percentage of successful test cases belong to a given class.

For our case we follow a slightly different approach than in the example of section \ref{sec:proptesting}: we include all test cases into the expected coverage, setting the second parameter always to \texttt{True} as well as the last argument, as we are only interested in testing the coverage, which is in fact the property we want to test. Implementing this property test is then simply a matter of computing the probabilities and of case analysis over the random input event and the agents output. It is important to note that in this property test we cannot randomise the model parameters $\beta$, $\gamma$ and $\delta$ because this would lead to random coverage. This might seem like a disadvantage but we do not really have a choice here, still the fixed model parameters can be adjusted arbitrarily and the property must still hold. We could have combined this test into the previous one but then we couldn't have used randomised model parameters. For this reason, and to keep the concerns separated, we opted for two different tests, which makes them also much more readable. 

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
prop_susceptible_prob :: Positive Double       -- current simulation time
                      -> NonEmptyList AgentId  -- population agent ids 
                      -> Property
prop_susceptible_prob (Positive t) (NonEmpty ais) = do
  -- fixed model parameters, otherwise random coverage
  let cor = 5     -- contact rate (beta)
      inf = 0.05  -- infectivity (gamma)
      ild = 15.0  -- illness duration (delta)
  -- compute distributions for all cases depending on event and SIRState
  -- frequencies; technical detail, omitted for clarity reasons
  let recoverPerc       = ...
      makeContPerc      = ...
      contactRecPerc    = ...
      contactSusPerc    = ...
      contactInfSusPerc = ...
      contactInfInfPerc = ...
  -- generate a random event
  evt <- genEvent ais
  -- run susceptible random agent with given parameters, only
  -- interested in its output SIRState, ignore id and events
  (_, ao, _) <- genRunSusceptibleAgent cor inf ild t ais evt
  -- encode expected distributions
  -- case analysis over random input events
  return $ property $ case evt of 
    Recover -> 
      cover recoverPerc True "Susceptible recv Recover" True
    MakeContact -> 
      cover makeContPerc True "Susceptible recv MakeContact" True
    (Contact _ Recovered) -> 
      cover contactRecPerc True "Susceptible recv Contact * Recovered" True
    (Contact _ Susceptible) -> 
      cover contactSusPerc True "Susceptible recv Contact * Susceptible" True
    (Contact _ Infected) -> 
      -- case analysis over resulting agent state
      case ao of
        Susceptible ->
          cover contactInfSusPerc True 
            "Susceptible recv Contact * Infected, stays Susceptible" True
        Infected ->
          cover contactInfInfPerc True 
            "Susceptible recv Contact * Infected, becomes Infected" True
        _ ->
          cover 0 True "INVALID" True
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

We have omitted the details of computing the respective distributions of the cases, which depend on the frequencies of the events and the occurrences of \texttt{SIRState} within the \texttt{Contact} event. By varying different distributions in the \texttt{genEvent} function, we can change the distribution of the test cases, leading to a more general test than just using uniform distributed events. When running the property test we get the following output:

\begin{footnotesize}
\begin{verbatim}
+++ OK, passed 100 tests (0.01s):
40% Susceptible recv MakeContact
25% Susceptible recv Recover
14% Susceptible recv Contact * Infected, stays Susceptible
12% Susceptible recv Contact * Susceptible
 9% Susceptible recv Contact * Recovered
    
Only 9% Susceptible recv Contact * Recovered, but expected 11%
Only 25% Susceptible recv Recover, but expected 33%
\end{verbatim}
\end{footnotesize}

QuickCheck runs 100 test cases, prints the distribution of the labels and issues warnings in the last two lines that generated and expected coverages differ in these cases. Further, not all cases are covered, for example the contact with an \texttt{Infected} agent and the case of becoming infected. The reason for these issues is insufficient testing coverage as 100 test cases are simply not enough for a statistically robust result. We could increase the number of test cases to 100,000, which \textit{might} cover all cases but could still leave QuickCheck not satisfied as the expected and generated coverage \textit{might} still differ.

\medskip

As a solution to this fundamental problem, we use QuickChecks \texttt{checkCoverage} function. As introduced in section \ref{sec:proptesting}, when the function \texttt{checkCoverage} is used, QuickCheck will run an increasing number of test cases until it can decide whether the percentage in \texttt{cover} was reached or cannot be reached at all. With the usage of \texttt{checkCoverage} we get the following output:

\begin{footnotesize}
\begin{verbatim}
+++ OK, passed 819200 tests (7.32s):
33.3292% Susceptible recv Recover
33.2697% Susceptible recv MakeContact
11.1921% Susceptible recv Contact * Susceptible
11.1213% Susceptible recv Contact * Recovered
10.5356% Susceptible recv Contact * Infected, stays Susceptible
 0.5520% Susceptible recv Contact * Infected, becomes Infected
\end{verbatim}
\end{footnotesize}

After 819,200 (!) test cases, run in 7.32 seconds on our hardware, QuickCheck comes to the statistically robust conclusion that the distributions generated by the test cases reflect the expected distributions and passes the property test.

\section{Encoding Model Invariants}
\label{sec:enc_model_inv}
By informally reasoning about the agent specification and by realising that they are, in fact, a state machine with a one-directional flow of \textit{Susceptible} $\rightarrow$ \textit{Infected} $\rightarrow$ \textit{Recovered} (as seen in Figure \ref{fig:sir_transitions}), we can come up with a few invariants, which have to hold for any SIR simulation run, \textit{under random model parameters} and independent of the random-number stream and the population:

\begin{enumerate}
	\item Simulation time is monotonic increasing. Each event carries a timestamp when it is scheduled. This timestamp may stay constant between multiple events but will eventually increase and must never decrease. Obviously, this invariant is a fundamental assumption in most simulations where time advances into the future and does not flow backwards.
	
	\item The number of total agents $N$ stays constant. In the SIR model no dynamic creation or removal of agents during simulation happens.
	
	\item The number of susceptible agents $S$ is monotonic decreasing. Susceptible agents \textit{might} become infected, reducing the total number of susceptible agents but they can never increase because neither an infected nor recovered agent can go back to susceptible.
	
	\item The number of recovered agents $R$ is monotonic increasing. This is because infected agents \textit{will} recover, leading to an increase of recovered agents but once the recovered state is reached, there is no escape from it.
	
	\item The number of infected agents $I$ respects the invariant of the equation $I = N - (S + R)$ for every step. This follows directly from the first property which says $N = S + I + R$.
\end{enumerate}

\subsection{Encoding the Invariants}
All these properties are expressed directly in code and read like a formal specification due to the declarative nature of functional programming:

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
sirInvariants :: Int                    -- N total number of agents
              -> [(Time,(Int,Int,Int))] -- output each step: (Time,(S,I,R))
              -> Bool
sirInvariants n aos = timeInc && aConst && susDec && recInc && infInv
  where
    (ts, sirs)  = unzip aos    -- split Time and (S,I,R) into 2 separate lists
    (ss, _, rs) = unzip3 sirs  -- split S, I and R into 3 separate lists

    -- 1. time is monotonic increasing
    timeInc = allPairs (<=) ts
    -- 2. number of agents N stays constant in each step
    aConst = all agentCountInv sirs
    -- 3. number of susceptible S is monotonic decreasing
    susDec = allPairs (>=) ss
    -- 4. number of recovered R is monotonic increasing
    recInc = allPairs (<=) rs
    -- 5. number of infected I = N - (S + R)
    infInv = all infectedInv sirs

    -- encodes property 2
    agentCountInv :: (Int,Int,Int) -> Bool
    agentCountInv (s,i,r) = s + i + r == n

    -- encodes property 5
    infectedInv :: (Int,Int,Int) -> Bool
    infectedInv (s,i,r) = i == n - (s + r)

    -- returns True if a predicate p is satisfied for all pairs in a list
    allPairs :: (Ord a, Num a) => (a -> a -> Bool) -> [a] -> Bool
    allPairs p xs = all (uncurry f) (pairs xs)

    -- pair up neighbouring elements of a list
    pairs :: [a] -> [(a,a)]
    pairs xs = zip xs (tail xs)
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

Putting this property into a QuickCheck test is straightforward. We randomise the model parameters $\beta$ (contact rate), $\gamma$ (infectivity) and $\delta$ (illness duration) because the properties have to hold for all positive, finite model parameters.

%\begin{HaskellCode}
\begin{footnotesize}
\begin{verbatim}
prop_sir_invariants :: Positive Int     -- beta (contact rate)
                    -> Probability      -- gamma (infectivity)
                    -> Positive Double  -- delta (illness duration)
                    -> TimeRange        -- random duration in range (0, 50)
                    -> [SIRState]       -- population
                    -> Property
prop_sir_invariants 
    (Positive beta) (P gamma) (Positive delta) (T t) as  = property (do
  -- total agent count
  let n = length as
  -- run the SIR simulation with a new RNG 
  ret <- genSimulationSIR as beta gamma delta t
  -- check invariants and return result
  return (sirInvariants n ret)
\end{verbatim}
\end{footnotesize}
%\end{HaskellCode}

Due to the large sampling space, we increase the number of test cases to run to 100,000 and all tests pass as expected. It is important to note that we put a random time limit within the range of (0,50) on the simulations to run. Meaning, that if a simulation does not terminate before that limit, it will be terminated at that random \texttt{t}. The reason for this is entirely practical as it ensures that the wall clock time to run the tests stays within reasonable bounds while still retaining randomness.